plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4' // Protobuf plugin
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets {
    main {
        proto {
            srcDirs = ['src/main/proto']
        }
        java {
            srcDirs = [
                'src',
                'build/generated/source/proto/main/grpc',
                'build/generated/source/proto/main/java'
            ]
        }
        resources {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
        resources {
            srcDir 'test'
        }
    }
}

dependencies {
    // JUnit Jupiter API and Engine
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    
    // JUnit Platform
    testImplementation 'org.junit.platform:junit-platform-launcher:1.10.2'
    
    // Mockito
    testImplementation 'org.mockito:mockito-core:5.10.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.10.0'
    
    // JUnit Jupiter Params
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    
    // JUnit 5
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'

    // gRPC and Protocol Buffers
    implementation 'io.grpc:grpc-netty:1.59.1'
    implementation 'io.grpc:grpc-protobuf:1.59.1'
    implementation 'io.grpc:grpc-stub:1.59.1'
    implementation 'com.google.protobuf:protobuf-java:3.25.1'
    implementation "io.grpc:grpc-services:1.59.1"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
    runtimeOnly "io.grpc:grpc-netty-shaded:1.59.1"
    testImplementation "io.grpc:grpc-testing:1.59.1"
    testImplementation "io.grpc:grpc-inprocess:1.59.1"
    testImplementation 'junit:junit:4.13.2'

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web:2.7.0'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:2.7.0'
}

def grpcVersion = '1.59.1'
def protobufVersion = '3.25.1'
def protocVersion = protobufVersion

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

test {
    useJUnitPlatform()
    
    filter {
        excludeTestsMatching "project.annotations.Assignment*"
    }
    
    testLogging {
        events "passed", "skipped", "failed"
    }
}

application {
    mainClass = 'project.Main' // Default main class
}

// Run task: for  DataStorageApiGrpcServer
task runDataStoreServer(type: JavaExec) {
    group = 'application'
    description = 'Run the DataStorageApiGrpcServer'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'project.apis.datastorage.DataStorageApiGrpcServer'
}

// Print the full runtime classpath for manual java runs
task printClasspath {
    doLast {
        println sourceSets.main.runtimeClasspath.asPath
    }
}